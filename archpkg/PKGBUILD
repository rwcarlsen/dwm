# $Id$
# Maintainer: Sergej Pupykin <rwcarlsen@gmail.com>

pkgname=dwm
pkgver=archv1
pkgrel=3
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2' 'dmenu' 'xosd')
install=dwm.install
source=(http://github.com/rwcarlsen/dwm/archive/$pkgver.tar.gz
	config.h
	dwm.desktop)
md5sums=('9292ae3e9fa34a6bd5eea9c8ce7bf8e8'
         '0e0c096a7106c4a26adafa31089bccb7'
         '939f403a71b6e85261d09fc3412269ee')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  cp $srcdir/config.h config.h
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
  install -m644 -D $srcdir/dwm.desktop $pkgdir/usr/share/xsessions/dwm.desktop
}
